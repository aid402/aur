# Maintainer: kev <noreply@datageek.info>
 
pkgname=shadowsocks-libev
pkgver=2.4.0
pkgrel=1
pkgdesc='A lightweight secured socks5 proxy for embedded devices and low end boxes.'
arch=('armv6h' 'armv7h')
url='https://github.com/shadowsocks/shadowsocks-libev'
license=('GPL3')
depends=('openssl' 'libev' 'libsodium')
source=("https://github.com/shadowsocks/shadowsocks-libev/archive/v$pkgver.tar.gz"
        'shadowsocks-libev@.service'
        'shadowsocks-libev-server@.service'
        'example.json')
 
prepare() {
  cd "$pkgname-$pkgver"
  rm -r libev libsodium
 
  sed -e 's|libev/Makefile||' \
      -e 's|m4_include(\[libev/libev.m4\])||' \
      -e 's|AC_CONFIG_SUBDIRS(\[libsodium\])||' \
      -i configure.ac
  sed -e 's| libev | |' \
      -e 's| libsodium | |' \
      -i Makefile.am
  sed -e 's|\$(top_builddir)/libev/libev.la||' \
      -e 's|-I\$(top_srcdir)/libev|-lev -lm|' \
      -e 's|\$(top_builddir)/libsodium/src/libsodium/libsodium.la||' \
      -e 's|-I\$(top_srcdir)/libsodium/src/libsodium/include|-lsodium|' \
      -i src/Makefile.am
}
 
build() {
  cd "$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}
 
package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
 
  install -Dm644 "$srcdir/example.json"  "$pkgdir/etc/shadowsocks/example.json"
  install -Dm644 "$srcdir/shadowsocks-libev@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-server@.service"
}

md5sums=('8711cfcbcc1d32daed102421a7c546b3'
         'e15494f943d1a7605054000acb0362ed'
         'a57ac46f329d0ad4f42366485d23181a'
         'a52e33ffe667a8d1a18fb0b5641b9b3a')
